apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    signingConfigs {
        debug {
            try {
                keyAlias 'android'
                keyPassword 'android'
                storeFile file("./jks/debug.jks")
                storePassword 'android'
                v2SigningEnabled false
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }
        release {
            try {
                keyAlias 'leo618'
                keyPassword JKS_PASSWORD
                storeFile file("./jks/leo.jks")
                storePassword JKS_PASSWORD
                v2SigningEnabled false
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        abortOnError false
        ignoreWarnings true
        disable 'MissingTranslation'
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.leo618.apf"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        resConfigs "zh"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }

    greendao {
        schemaVersion 1  //版本号，数据需要升级时配置
    }

    // 多渠道列表
    productFlavors {
        GF { manifestPlaceholders = [UMENG_CHANNEL_VALUE: "GF"] }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.alibaba:fastjson:1.2.7'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.lzj.appupdate:library:0.0.5'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.6'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.leo618.utils:utils:0.0.1'
}
